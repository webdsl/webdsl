module org/hql/dsl/syntax/pp-hql

imports
  libstratego-gpp // ??

strategies

  hql-to-textfile(|file) =
    !TextFile(file, <pp-hql-to-string>)
    
  output-hql-to-file = 
    let open-file   = <open-stream> (<id>, "w") <+ perror; <exit> 1
     in where( fout := <get-config; open-file <+ stdout-stream> "-o" )
        ; finally( pp-hql-to-stream(|fout), <fclose> fout )
    end

  /**
   * Pretty-prints the hql AST to a string.
   *
   * @type hql -> String
   */
  pp-hql-to-string =
    hql-to-abox
    ; box2text-string(|80)

  /**
   * Pretty-prints the hql AST to a Stream.
   *
   * @param Stream
   * @type hql -> Stream
   */
  pp-hql-to-stream(|stream) =
    hql-to-abox
    ; box2text-stream(|80, stream)

  /**
   * Pretty-prints the hql AST to a Box representation.
   *
   * @type hql -> Box
   */
    
  hql-to-abox =
    ast2box(|[<hql-pp-table>])
    
  hql-pp-table =
    HqlPpTable
    <+ tbl := <import-term(HQL-pretty.pp.af)>
       ; rules( HqlPpTable := tbl )
       ; !tbl
