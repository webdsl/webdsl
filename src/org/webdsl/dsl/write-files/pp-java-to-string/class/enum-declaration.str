module org/webdsl/dsl/write-files/pp-java-to-string/class/enum-declaration

imports
  libstratego-lib
  libjava-front

rules

  java-to-string:
    EnumDec(head, body) -> <java-to-string-all> [head, body]

  java-to-string:
    EnumDecHead(mods, Id(name), implements)
      -> <java-to-string-all> [mods, "enum", name, implements]

  java-to-string:
    EnumBody(consts, body) -> <block-structure-string> (1, [consts2, body2])
    with  <sep-by-comma> consts => consts2
        ; (!body => None() < ![] +  ![body]) => body2

  java-to-string:
    EnumConst(Id(s), None(), None()) -> s

  java-to-string:
    EnumConst(Id(s), Some(args), None()) -> <java-to-string-all> [s, args']
    with  <list-to-args-string> args => args'

  java-to-string:
    EnumConst(Id(s), args, Some(body)) -> <java-to-string-all> [enum, body]
    with  !EnumConst(Id(s), args, None()) => enum

  java-to-string:
   EnumBodyDecs(decs) -> <java-to-string-all> [";\n", decs]
