module org/webdsl/dsl/write-files/pp-java-to-string/interface/annotation-type

imports
  libstratego-lib
  libjava-front

rules

  java-to-string :
    AnnoDec(head, decs) -> <java-to-string-all> [head, decs']
    with  <block-structure-string> (1, decs) => decs'

  java-to-string:
    AnnoDecHead(mods, Id(name))
      -> <java-to-string-all> [annos, mods', "@interface", name]
    with  <  split-fetch-keep(not(is-Java-Anno))
             ; \ (l1, t, l2)  -> (l1, [t | l2]) \

          <+ !(<map(is-Java-Anno)>, [])

          <+ !([], <id>)
          > mods => (annos, mods')

  java-to-string :
    AnnoMethodDec(mods, type, Id(name), default)
      -> <java-to-string-all> [mods, type, " ", name, "(", ")", default', ";\n"]
    with  let default-to-boxes =
                \ None() -> [] \
              + \ Some(d) -> [d] \

           in <default-to-boxes> default => default'
          end

  java-to-string :
    DefaultVal(val) -> <java-to-string-all> ["default", val]
