module org/webdsl/dsl/write-files/pp-java-to-string/literal

imports
  libstratego-lib
  libjava-front

rules

  java-to-string: True()      -> "true"
  java-to-string: False()     -> "false"
  java-to-string: Null()      -> "null"
  java-to-string: VoidClass() -> "void.class"
  java-to-string: Class(type) -> <java-to-string-all> [type, ".", "class"]

  java-to-string: Bool(b)  -> <java-to-string> b
  java-to-string: Char(c)  -> <java-to-string-all> ["'", c, "'"]

  java-to-string: Single(c) -> s
  where <implode-string> [c] => s

rules

  java-to-string:
    NamedEscape(c) -> s
    where <implode-string> ['\', c] => s

  java-to-string:
    UnicodeEscape(us, c1, c2, c3, c4) -> s
    where <implode-string> ['\' | <conc> (us, [c1, c2, c3, c4])] => s

  java-to-string:
    OctaEscape1(c1) -> s
    where <implode-string> ['\', c1] => s

  java-to-string:
    OctaEscape2(c1, c2) -> s
    where <implode-string> ['\', c1, c2] => s

  java-to-string:
    OctaEscape3(c1, c2, c3) -> s
    where <implode-string> ['\', c1, c2, c3] => s

rules

  java-to-string: String(parts) -> <java-to-string-all> ["\"", parts, "\""]
  java-to-string: Chars(s) -> s

rules

  java-to-string: Float(f)  -> f
  java-to-string: Deci(d)   -> d
  java-to-string: Hexa(h)   -> h
  java-to-string: Octa(o)   -> o
