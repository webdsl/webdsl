module org/webdsl/dsl/languages/ui/htmlwrapper

imports
  libstrategolib
  libwebdsl-front

rules

 normalize-constructor :
   HTMLWrapper(x, farg*, elemname, HTMLWrapperAssigns(prop*)){anno*}
   ->
   Define([Template()], x, farg*, None(),
     [ XMLElement(
        elemname
      , [prop*, AllAttributes()]
      , [TemplateCall("elements", [], [], TemplateBody([]))]
      , elemname
      )
    ]
  ){anno*}

  normalize-constructor :
    Section(sname, defs1) -> Section(sname, defs2)
    where <fetch(?HTMLWrapperMultiple(_))> defs1
    with  (body*,rest*) := <partition(?HTMLWrapperMultiple(<id>))> defs1
        ; newtemplate* := <concat; map(\HTMLWrapperBody(a,b,c,d){anno*} -> HTMLWrapper(a,b,c,d){anno*}\)> body*
        ; defs2 := [newtemplate*, rest*]  
