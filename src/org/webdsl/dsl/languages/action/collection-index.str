module org/webdsl/dsl/languages/action/collection-index

rules

  constraint-error-action :
    CollectionIndex(e_col,e_index) -> <add-error(|["Indexed access [] is only allowed for List<type>, type found: ",<pp-type>srt,"."])>
    where not(type-of)
        ; srt := <type-of> e_col; <type-of> e_index // don't report when sub-expression was already wrong

  constraint-error-action :
    CollectionIndex(e_col,e_index) -> <add-error(|["Index must be of type Int, but found type '",<pp-type>type,"'."])>
    where type := <type-of> e_index //check that it has a type, otherwise error is already reported
        ; not(SimpleSort("Int") := type) //but not the correct one

  eq-type-of-action :
    CollectionIndex(e_col,e_index) -> srt
    where GenericSort("List",[srt]) := <type-of> e_col
  
//need to distinguish collectionindex in an assignment because it is desugared to a call to "set"
signature constructors
  CollectionIndexInAssignment : Term
rules        
        
  rename-action: 
    Stat(Assignment(CollectionIndex(e_col, e_index), e_val)) ->
    Stat(Assignment(CollectionIndex(<rename-all> e_col, <rename-all> e_index){CollectionIndexInAssignment()}, <rename-all> e_val)) 
            
rules
    
  desugar-action :
    CollectionIndex(e_col,e_index){anno*} -> Call(e_col,"get",[e_index])
    where not(<fetch(CollectionIndexInAssignment)> anno*)

  desugar-action :
    Stat(Assignment(CollectionIndex(e_col, e_index){anno*}, e_val)) -> Stat(Call(e_col,"set",[e_index,e_val]))
    where <fetch(CollectionIndexInAssignment)> anno*
    