module org/webdsl/dsl/modules/types/file

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  
imports 
  libwebdsl-front
    
rules
  is-simple-sort = ?SimpleSort("File")
  extends-builtin : "File" -> "Object"
  built-in-template-rule-setup = <declare-built-in-template-rules> ("outputFile",[SimpleSort("File")]);fail
  built-in-template-rule-setup = <declare-built-in-template-rules> ("downloadLink",[SimpleSort("String"),SimpleSort("Void")]);fail
    
rules

  type-default-value :
    SimpleSort("File") -> exp|[ null ]|
    
rules // Desugaring
      
  DeriveOutput :
    elem|[ output(e)[passign*]{} ]| -> elem|[ outputFile(e)[passign*]{} ]|
    where SimpleSort("File") := <type-of> e
    
  desugar-in-define-context : //after desugaring, this rule calls rename on the whole define
    elem|[ outputFile(e)[passign*]{} ]| -> elem|[ downloadlink action{e.download();} {output(e.fileName())} ]|
    
rules //file download

  check-builtin-signature :
    (SimpleSort("File"), "download", []) ->  SimpleSort("Void")
    
  check-builtin-signature :
    (SimpleSort("File"), "fileName", []) ->  SimpleSort("String")

    
