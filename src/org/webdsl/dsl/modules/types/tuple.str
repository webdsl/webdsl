module org/webdsl/dsl/modules/types/tuple

imports
  libstrategolib
  libwebdsl-front
  org/webdsl/dsl/typechecker/-

rules

  subtype-of =
    ?(TupleSort(_), SimpleSort("Object"))

  type-exists = ?TupleSort(_)

  type-compatible =
    ?(t1, SimpleSort("Null"))
    ; where(<?TupleSort(_)> t1)

rules

  type-compatible =
    ?(TupleSort(types1), TupleSort(types2))
  ; where( <zip; map(type-compatible)> (types1, types2) )

  eq-type-of :
    TupleCreation(e*) -> TupleSort(t*)
    where t* := <map(type-of)> e*

rules

  eq-type-of :
    FieldAccess(e1, "length") -> <add-immutable-anno> SimpleSort("Int")
    where TupleSort(_) := <type-of> e1

  eq-type-of :
    FieldAccess(e1, number) -> <index(|<int-add(|1)> index); add-immutable-anno> t*
    where index := <is-tuple-index-accessor> number
    where TupleSort(t*) := <type-of> e1
