module org/webdsl/dsl/modules/types/string

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules
  is-simple-sort = ?SimpleSort("String")  
  extends-builtin : "String" -> "Object"
  
  //to allow static functions
  built-in-template-rule-setup = declare-native-class-static-functions(|"String","java.lang.String");fail
  
rules

  type-default-value :
    SimpleSort("String") -> exp|[ "" ]|
  
  eq-type-of :
    String(x) -> SimpleSort("String")

  check-builtin-signature :
    (_, "toString", []) -> SimpleSort("String")

rules //native string operations

 check-builtin-signature :
    (GenericSort("List",[SimpleSort("String")]), "concat", []) ->  SimpleSort("String")
    
 check-builtin-signature :
    (GenericSort("List",[SimpleSort("String")]), "concat", [SimpleSort("String")]) ->  SimpleSort("String")

 check-builtin-signature :
    (None(), "log", [s]) ->  SimpleSort("Void")
     
rules // hook to specify string-compatible types, e.g. 'string-compatible = ?SimpleSort("Text")'

  type-compatible = ?(<StringCompatible>, <StringCompatible>)

  make-string-compatible =
    typename := <id>
  ; rules( 
      StringCompatible : SimpleSort(typename)
      StringCompatibleTypeNames :+= typename
    )
  
  built-in-template-rule-setup = <make-string-compatible> "String"; fail
  