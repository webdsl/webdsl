module org/webdsl/dsl/to-java-servlet/uicomponents/xml-content
    
rules
              
  elem-to-java-servlet :
    XMLEmptyElement(name,attrs) -> <xml-elem-to-java(|"<","/>")> (name,attrs) 
              
  elem-to-java-servlet :
    XMLElement(name1,attrs1,elem*,name2) ->
    bstm* |[ 
      bstm_start*
      bstm*
      bstm_end*
    ]|
    with  bstm_start* := <xml-elem-to-java(|"<",">")> (name1,attrs1) 
        ; bstm_end* := <xml-elem-to-java(|"</",">")> (name2,[]) 
        ; bstm* := <elems-to-java-servlet> elem* 
  
  xml-elem-to-java(|open,close) :
    (name,attrs) ->
    bstm* |[ 
      out.print("~open"+"~name");
      bstm*
      out.print("~close");
    ]|
    with bstm* := <map(xml-attrs-to-java);concat> attrs  
        
  xml-attrs-to-java :
    Attribute(name,value) ->
    bstm*|[
      out.print(" "+"~name"+"=\"");
      bstm*
      out.print("\"");
    ]|
    with bstm* := <to-page-string-java-servlet> value
    
  //also support all attributes, all attributes except, etc.
  xml-attrs-to-java :
    x -> bstm*|[ out.print(e); ]|
    where e := <convert-attribute-selectors-to-java-exp> x
    
rules //if(){} and if(){}else{} inside an XML element containing a list of attributes
  
  xml-attrs-to-java :
    XMLAttributesIf(e1,attrs) ->
    bstm*|[
      if(e_check){
        bstm*
      }
    ]|
    with bstm* := <map(xml-attrs-to-java);concat> attrs
       ; e_check := <expression-to-java-servlet> e1
  
  xml-attrs-to-java :
    XMLAttributesIfElse(e1,attrsif,attrselse) ->
    bstm*|[
      if(e_check){
        bstm_if*
      }
      else {
        bstm_else*
      }
    ]|
    with bstm_if* := <map(xml-attrs-to-java);concat> attrsif
       ; bstm_else* := <map(xml-attrs-to-java);concat> attrselse
       ; e_check := <expression-to-java-servlet> e1
    
    //