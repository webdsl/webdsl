module org/webdsl/dsl/to-java-servlet/email/email

imports
  libstratego-lib
  libjava-front
  libwebdsl-front
  org/webdsl/dsl/to-java-servlet/- 

rules

  call-to-java-servlet :
    tc@SendEmailFunctionCall(EmailCall(x_name, args)) ->
    expr |[ utils.ThreadLocalPage.get().sendEmail("~x_name", e_args, new org.webdsl.lang.Environment(utils.ThreadLocalPage.getEnv())) ]|
    with  e_args := <map(expression-to-java-servlet); optimize-empty-array> args

  call-to-java-servlet :
    tc@RenderEmailFunctionCall(EmailCall(x_name, args)) ->
    expr |[ ((utils.PageServlet)utils.ThreadLocalPage.get()).renderEmailAndCreateQueuedEmail("~x_name", e_args, new org.webdsl.lang.Environment(utils.ThreadLocalPage.getEnv())) ]|
    with  e_args := <map(expression-to-java-servlet); optimize-empty-array> args

  element-to-java-servlet:
    elem |[ to(e)[passign*]{} ]| ->
    <element-render(| 
      bstm* |[ this.receivers = e1;  ]|
    )>
    with  e1 := <expression-to-java-servlet> e

  element-to-java-servlet:
    elem |[ subject(e)[passign*]{} ]| ->
    <element-render(| 
      bstm* |[ this.subject = e1;  ]|
    )>
    with  e1 := <expression-to-java-servlet> e

  element-to-java-servlet:
    elem |[ from(e)[passign*]{} ]| -> 
    <element-render(|
      bstm* |[ this.sender = e1;  ]|
    )>
    with  e1 := <expression-to-java-servlet> e
    
  element-to-java-servlet:
    elem |[ cc(e)[passign*]{} ]| -> 
    <element-render(|
      bstm* |[ this.cc = e1;  ]|
    )>
    with  e1 := <expression-to-java-servlet> e
    
  element-to-java-servlet:
    elem |[ bcc(e)[passign*]{} ]| -> 
    <element-render(|
      bstm* |[ this.bcc = e1;  ]|
    )>
    with  e1 := <expression-to-java-servlet> e
    
  element-to-java-servlet:
    elem |[ unsubscribeAddress(e)[passign*]{} ]| -> 
    <element-render(|
      bstm* |[ this.unsubscribeAddress = e1;  ]|
    )>
    with  e1 := <expression-to-java-servlet> e
    
  element-to-java-servlet:
    elem |[ replyTo(e)[passign*]{} ]| ->
    <element-render(| 
      bstm* |[ this.replyTo = e1;  ]|
    )>
    with  e1 := <expression-to-java-servlet> e
