module org/webdsl/dsl/to-java-servlet/statistics

imports
  libstratego-lib
  libjava-front
  libwebdsl-front

rules

  generate-code-java-servlet-once: _ ->
    <emit-java-code; fail> compilation-unit|[
      package utils;

      import pkgname.*;

      import java.util.ArrayList;
      import java.util.List;

      public class Statistics {

        public static void logStatistics() {
          List<PageStats> results = new ArrayList<PageStats>();

          ~bstm*:bstm_pages*

          utils.PageStats.printList(results);
        }

      }
      ]|
    with pkgname    := <TemplatePackage>
       ; pkgtname_domain := <DomainPackage>
       ; ambname_domain := pkgtname_domain
       ; page* := <bagof-AllPageNames; handle-overrides-by-name>
       ; ajaxtemplate* := <bagof-AllTopLevelTemplateNamesWithAjax; string-sort-annos>
       ; bstm_pages* := <map({\
                           x -> bstm |[
                             results.add(new PageStats().setPage("~x")
                               .setReadOnly(x_value.renderReadOnly.getAndSet(0))
                               .setReadOnlyFromCache(x_value.renderReadOnlyFromCache.getAndSet(0))
                               .setWithUpdates(x_value.renderWithUpdates.getAndSet(0))
                               .setActionFailed(x_value.actionFailed.getAndSet(0))
                               .setActionSuccessReadOnly(x_value.actionSuccessReadOnly.getAndSet(0))
                               .setActionSuccessWithUpdates(x_value.actionSuccessWithUpdates.getAndSet(0)));
                           ]|
                           with  x_value := <java-servlet-page-class-name> x
                       \})>
                       [page*, ajaxtemplate*]
