module org/webdsl/dsl/to-java-servlet/function-exp

rules

  builtin-java-type : FunctionSort(_,_) -> type|[ org.webdsl.lang.Function ]|
  
rules
  
  expression-to-java-servlet :
    FunctionRefCallPartial(fr@FunctionRef(x,fargtypes,srt),extraArgs) ->
    java:expr |[ new org.webdsl.lang.Function(new webdsl.generated.functions.x_classname()).partial(new Object[]{e*}) ]|
    with x_classname := <get-function-class-name-from-functionref> fr
    with e* := <map(expression-to-java-servlet)> extraArgs
  
  get-function-class-name-from-functionref : FunctionRef(x,fargtypes,srt) -> <concat-strings> [x,"_"] 
  //<concat-strings> [<new-function-name> (x,fargtypes),"_"]
  
  expression-to-java-servlet :
    FunctionRefCall(fr@FunctionRef(x,fargtypes,srt),args) ->
    java:expr |[ new org.webdsl.lang.Function(new webdsl.generated.functions.x_classname()).apply(new Object[]{e*}) ]|
    with x_classname := <get-function-class-name-from-functionref> fr
    with e* := <map(expression-to-java-servlet)> args 
    
  expression-to-java-servlet :
    fr@FunctionRef(x,fargtypes,srt) ->
    java:expr |[ new org.webdsl.lang.Function(new webdsl.generated.functions.x_classname()) ]|
    with x_classname := <get-function-class-name-from-functionref> fr
    
  call-to-java-servlet :
    ThisCall(f, e1*) -> java:expr |[ (x_type)e.apply(new Object[]{e2*}) ]| 
    where FunctionSort(args,srt) := <get-type-anno> f
    with  e := <expression-to-java-servlet> Var(f)
    with  e2* := <map(expression-to-java-servlet)> e1*
    with  x_type := <java-type-name> srt
    