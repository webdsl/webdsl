 module org/webdsl/dsl/to-java-servlet/java-page

imports
  libstratego-lib
  libjava-front

imports
  libwebdsl-front
  org/webdsl/dsl/to-java-servlet/to-java-servlet

rules

  java-servlet-page-class-name = 
  	java-servlet-page-name; make-java-class-name-compliant
  	
  java-servlet-page-name :
    x -> <concat-strings> [x1,"Page"]
    with  x1     := <capitalize-string> x
 /*
  java-servlet-page-template-name :
    x -> <concat-strings> [x1,"Page","Template"]
    with  x1     := <capitalize-string> x

  java-servlet-template-name :
    x -> <concat-strings> [x1,"Template"]
    with  x1     := <capitalize-string> x

  java-servlet-local-template-name :
    (x, x_pagetempl) -> <concat-strings> [x2,x1,"LocalTempl"]
    with  x1     := <capitalize-string> x
        ; x2     := <capitalize-string> x_pagetempl
*/
rules

  page-class-generation-test =
    where (
      not(ThisEmail)
    ; get-page-class-name
    )

  get-page-class-name = (JavaThisPage <+ ThisAjaxTemplateName); java-servlet-page-class-name

  generate-java-servlet-partial =
    ? cbds
    ; page-class-generation-test
    ; pkgname := <TemplatePackage>
    ; x_Page  := <get-page-class-name>
    //also allow a single class-body-declaration
    ; with (
        if <is-list> cbds then cbds* := cbds else cbds* := [cbds] end
      ; <emit-java-code-local>
          compilation-unit|[
            package pkgname;

            @Partial
            public class x_Page extends utils.PageServlet{
              ~*cbds*
            }
          ]|
    )
    ; !cbds

rules
  /**
   * Generate base bean code
   */
  generate-code-java-servlet-template = generate-java-servlet-page; fail

  generate-java-servlet-page :
    def@def|[ define mod* x_page (farg*)  req*  { elem* } ]| ->
    <emit-java-code-local> compilation-unit|[
        package pkgname;

        import java.util.concurrent.atomic.AtomicInteger;

        @Partial
        @SuppressWarnings("all")
        public class x_Page extends utils.PageServlet
        {

          @Partial
          protected void initialize(){}

          @Partial
          protected void conversion(){}

          @Partial
          protected void loadArguments () {}//previously argumentInitialize

          public String getPageName()
          {
            return "~x_page";
          }

          /* problem with merging? */
          @Partial
          public void hiddenParams(java.lang.StringBuilder combinedparams){}

          public String getHiddenParams()
          {
            java.lang.StringBuilder hiddens = new java.lang.StringBuilder(512);
            hiddenParams(hiddens);
            return hiddens.toString();
          }

          @Partial
          public void urlQueryParams(java.lang.StringBuilder combinedparams){}

          public String getUrlQueryParams()
          {
            java.lang.StringBuilder params = new java.lang.StringBuilder(512);
            urlQueryParams(params);
            return params.toString();
          }

          @Partial
          public void hiddenPostParamsJson(java.lang.StringBuilder combinedparams){}

          public String getHiddenPostParamsJson()
          {
            java.lang.StringBuilder hiddens = new java.lang.StringBuilder(512);
            hiddenPostParamsJson(hiddens);
            return hiddens.toString();
          }

          public void initVarsAndArgs(){
            utils.GlobalVariables.initGlobalVars(envGlobalAndSession, e_HibSession);
            initialize();
            conversion();
            loadArguments();

            Object[] args = ~<optimize-empty-array> e*;
            this.args = args;
          }

          protected void initTemplateClass(){
            templateservlet = new x_specialpagetemplate();
            ~bstm*:bstm_readonly*
          }

          protected void addPrincipalToRequestLog(org.webdsl.WebDSLEntity rle){
            ~*<if AccessControlEnabled then ![ bstm*|[ ((pkgtname_domain.RequestLogEntry)rle).setPrincipalNoEventsOrValidation(((pkgtname_domain.SecurityContext)envGlobalAndSession.getSessionVariable("securityContext")).getPrincipal()); ]| ] else ![] end>[]
          }

          protected org.webdsl.WebDSLEntity getRequestLogEntry(){
              return ((utils.DispatchServletHelper) utils.ThreadLocalServlet.get()).getRequestLogEntry();
          }

          protected void addLogSqlToSessionMessages(){
            if(this.isLogSqlEnabled()){ // Cannot use (parammap.get("logsql") != null) here, because the parammap is cleared by actions
              if(logSqlCheckAccess()){
                ((pkgtname_domain.SessionManager)utils.ThreadLocalServlet.get().getSessionManager()).setLogsqlMessageNoEventsOrValidation(utils.HibernateLog.printHibernateLog(this, "action"));
              }
              else{
                ((pkgtname_domain.SessionManager)utils.ThreadLocalServlet.get().getSessionManager()).setLogsqlMessageNoEventsOrValidation("Access to SQL logs was denied.");
              }
            }
          }

          protected void initializeBasics(utils.AbstractPageServlet ps, Object[] args) {
            initTemplateClass();

            this.envGlobalAndSession = ps.envGlobalAndSession;
            this.request=ps.getRequest();
            this.response=ps.getResponse();
            
            // when rendering an ajax template, ignore request parameters from page/ajax submit request that triggers the rendering
            this.parammap = java.util.Collections.<String, String>emptyMap();
            this.parammapvalues = java.util.Collections.<String, java.util.List<String>>emptyMap();    
            this.fileUploads = java.util.Collections.<String, java.util.List<utils.File>>emptyMap();
            
            org.hibernate.Session hibSession = e_HibSession;
            utils.ThreadLocalServlet.get().loadSessionManager(e_HibSession);

            ~bstm*:bstm_args*

            initialize();

          }

          protected String[] getUsedSessionEntityJoins() {
            return e_SessionPrefetch;
          }

          protected void renderDebugJsVar(java.io.PrintWriter sout){
            ~*<!bstm*|[
                sout.println("<script type=\"text/javascript\">var show_webdsl_debug=false;</script>");
              ]|
            ; if-debug(!bstm*|[
                sout.println("<script type=\"text/javascript\">var show_webdsl_debug=true;</script>");
              ]|)>[]
              
              ~*< if is-debug-ac then 
		                !bstm*|[
		                  sout.println(
		                    "<script type=\"text/javascript\">document.addEventListener('DOMContentLoaded', function() { " +
		                      "$('.ac_info').mouseup( function() { if(window.getSelection && window.getSelection().toString().length < 1) { $(this).hide() } } ); " +
		                      "$('body').append(\"<a href=\\\"javascript:$('.ac_info').toggleClass('visible'); void(0);\\\" class=\\\"debug-toggle\\\">Show AC info</a>\");" + 
		                    "} );</script>"+
		                    "<style>.debug-toggle { position: fixed; background-color: orange; padding: 3px; bottom: 10px; left: 10px; }" +
			                    ".ac_check { overflow: hidden; display: inline-block;border: 1px dashed #ababab; margin: 1px; }"+
			                    ".ac_check:hover{ border-color: red; border-style: solid ; }"+
			                    ".ac_check .ac_info { white-space: pre; display: none; }"+
			                    ".ac_check:hover>.ac_info.visible { display: block; border: 1px solid gray; position: absolute; float: left; font-size: 10px; padding: 2px 10px 2px 10px; background-color: #ececec; z-index: 1000000; }"+
			                  "</style>");
		                ]|
                  end >[]

          }

          protected boolean isTemplate() { return e_istemplate; }

          public String getUniqueName() {
            return "~x_page";
          }
          
          ~*<try(cbds-is-web-service(|mod*))>[]

          // statistics to be shown in log
          protected void increaseStatReadOnly() { renderReadOnly.incrementAndGet(); }
          protected void increaseStatReadOnlyFromCache() { renderReadOnlyFromCache.incrementAndGet(); }
          protected void increaseStatUpdate() { renderWithUpdates.incrementAndGet(); }
          protected void increaseStatActionFail() { actionFailed.incrementAndGet(); }
          protected void increaseStatActionReadOnly() { actionSuccessReadOnly.incrementAndGet(); }
          protected void increaseStatActionUpdate() { actionSuccessWithUpdates.incrementAndGet(); }
          public static AtomicInteger renderReadOnly = new AtomicInteger();
          public static AtomicInteger renderReadOnlyFromCache = new AtomicInteger();
          public static AtomicInteger renderWithUpdates = new AtomicInteger();
          public static AtomicInteger actionFailed = new AtomicInteger();
          public static AtomicInteger actionSuccessReadOnly = new AtomicInteger();
          public static AtomicInteger actionSuccessWithUpdates = new AtomicInteger();

        }
      ]|
    where x_Page    := <get-page-class-name>
        ; page-class-generation-test
    with pkgname    := <TemplatePackage>
       ; pkgname_domain   := <DomainPackage>
       ; pkgtname_domain  := pkgname_domain
       ; x_specialpagetemplate := <ThisTemplateClassName>
       ; sents := <get-anno(?SessionEntities(<id>))> def
       ; e_SessionPrefetch := <get-session-prefetch> sents
       ; e* := <filter({\arg@farg|[ x : srt ]| -> var-init|[ x ]| \})> farg* //just pass args of page to special pagetemplate
       ; bstm_args* := <add-indices;filter({\(index,arg@farg|[ x : srt ]|) -> bstm|[ x = (x_arg) args[x_index]; ]|
                                         where x_index := <subti;int-to-string> (index,1)
                                             ; if <?Arg(_,RefSort(_))> arg
                                               then x_arg := "utils.RefArg"
                                               else x_arg := <argument-java-servlet-type> arg end
                                      \})> farg* //variables are generated in java-template-vars
       ; e_istemplate := <JavaThisPage; !java|[ false ]| <+  !java|[ true ]|>
       ; if <fetch(?ReadOnlyPage)> mod*
         then bstm_readonly* := java:bstm*|[ this.isReadOnly = true; ]|
         else bstm_readonly* := [] end
       
  cbds-is-web-service(|mod*) =
    <fetch(?WebService)> mod*
  ; !class-body-dec* |[
      public boolean isWebService(){
        return true;
      }
    ]|

  /**
   *  Used for output(Image) and output(File)
   */
  action-call-with-get-param-name = !"action-call-with-get-request-type"

  /**
   *  Used for pages/services that have POST data (thus is not an action)
   */
   post-without-action-param-name = !"post-request-no-action"

  get-session-prefetch = !expr |[ null ]|
    /*?sents
    ; prefetches := <JavaThisPagePrefetches <+ ![]>
    ; <filter(get-session-prefetch(|prefetches))> sents
    ; flatten-list
    ; string-list-to-java-servlet-or-null*/

  get-session-prefetch(|prefetches) :
    sent -> joins
    where pf := <get-prefetch-for(|sent)> prefetches
    ; not(None() := pf)
    ; joins := <get-to-one-joins; add-session-property-to-joins(|sent)> pf

  add-session-property-to-joins(|sent) :
    [] -> [join]
    with join := <concat-strings> ["_", sent]

  add-session-property-to-joins(|sent) :
    [join|joins] -> [join2|joins2]
    with join2 := <concat-strings> ["_", sent, ".", join]
    ; joins2 := <add-session-property-to-joins(|sent)> joins
