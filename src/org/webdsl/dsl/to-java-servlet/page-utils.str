module org/webdsl/dsl/to-java-servlet/page-utils

imports
  libstratego-lib
  libjava-front
  libwebdsl-front

rules // embedding expressions in presentation

  /**
   * Converts an arbitrary WebDSL expression into java statement
   * that produce a string for displaying it on a page.
   *
   * Mainly used to prevent null-pointer exceptions breaking the entire page and applying a filter for HTML related characters.
   */
  to-page-string-java-servlet :
    e -> java:bstm|[ 
      try {
        utils.RenderUtils.printPageString(e1, out, threadLocalPageCached.isRawoutput());
      }
      catch(NullPointerException npe){
        utils.Warning.warn("null reference prevented rendering of template element");
      }
      catch(IndexOutOfBoundsException ine){
        utils.Warning.warn("index out of bounds prevented rendering of template element");
      }
    ]|
    with  e1 := <expression-to-java-servlet> e

  to-page-string-java-servlet-no-filter :
    e -> java:bstm|[ 
      try {
        utils.RenderUtils.printPageString(e1, out, true);
      }
      catch(NullPointerException npe){
        utils.Warning.warn("null reference prevented rendering of template element");
      }
      catch(IndexOutOfBoundsException ine){
        utils.Warning.warn("index out of bounds prevented rendering of template element");
      }
    ]|
    with  e1 := <expression-to-java-servlet> e

  to-page-string-java-servlet-skip-try :
    e -> java:bstm|[
      utils.RenderUtils.printPageString(e1, out, threadLocalPageCached.isRawoutput());
    ]|
    with  e1 := <expression-to-java-servlet> e

rules

  hide-bstms-on-null-pointer-exception :
    bstm* -> 
    bstm* |[
      java.io.StringWriter x_sw = new java.io.StringWriter();
      out = new java.io.PrintWriter(x_sw); 
      utils.ThreadLocalOut.push(out);
      boolean x_su = false;
      try {
        bstm*
        x_su = true;
      }
      catch(NullPointerException npe) {
        //utils.Warning.warn("Null reference caused template element to be hidden: "+"~ppterm");
        utils.Warning.warn("null reference prevented rendering of template element");
      }
      catch(IndexOutOfBoundsException ine){
        utils.Warning.warn("index out of bounds prevented rendering of template element");
      }
      utils.ThreadLocalOut.popChecked(out);
      out = utils.ThreadLocalOut.peek();
      if(x_su){
        out.print(x_sw.toString());
      }
    ]|
    with x_sw := <newname-webdsl> "sw"
       ; x_su := <newname-webdsl> "succeeded"
       //; ppterm := <pp-webdsl-to-string; escape-chars(Escape-double-quote<+Escape-backslash)> term
       