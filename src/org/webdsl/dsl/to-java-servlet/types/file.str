module org/webdsl/dsl/to-java-servlet/types/file

imports 
  libstrategolib 
  libjava-front 

imports 
  libwebdsl-front

rules

  builtin-java-type :
    SimpleSort("File") -> type|[ utils.File ]|

  //used in entity generation
  //hibernate has a built-in mapping for java.sql.Blob
  type-specific-annotations :
    Property(x_prop, k, s, annos) -> <file-specific-annotations>
    where <?SimpleSort("File")> s

  file-specific-annotations =
    ![ anno |[ @OneToOne(fetch = javax.persistence.FetchType.LAZY) ]|
     , anno |[ @org.hibernate.annotations.Cascade({org.hibernate.annotations.CascadeType.PERSIST, org.hibernate.annotations.CascadeType.SAVE_UPDATE, org.hibernate.annotations.CascadeType.MERGE}) ]|
     ]

rules

  java-servlet-element-id(|suffix) :
    tc@DownloadLink(_,_,_){anno*} -> tc{ActionNumber(newid),anno*} 
    where  newid := <newname-webdsl-suffix(|suffix)> "action"

rules

  element-to-java-servlet = ?DownloadLink(_,_,_); element-databind-action-render(|<databind-phase-downloadlink>, <action-phase-downloadlink>, <render-phase-downloadlink>)

  //outputFile is already desugared, see modules/types/file.str

  //for downloading files, a modified action call is used

  render-phase-downloadlink :
    tc@DownloadLink(ActionCall(_, args), passign*, elem*) ->
    bstm* |[ 
    out.print("<a href=\"javascript:void(0)\" onclick='javascript:serverInvokeDownloadCompatible(\""+threadLocalPageCached.getAbsoluteLocation()+"/" + threadLocalPageCached.getActionTarget() + threadLocalPageCached.getExtraQueryAruments("?") + "\",\""+ e_ident+"\", ["+threadLocalPageCached.getHiddenPostParamsJson()+"],\""+threadLocalPageCached.getFormIdent()+"\", this)'");
    bstm_attr
    out.print(">");
    bstm*
    out.print("</a>");
    ]|
    with  bstm* := <elements-to-java-servlet> elem*
        ; e_ident := <action-id-java-expression(|args, passign*)> tc
        ; bstm_attr := <to-html-attrs-include-collection(|"downloadlink")> passign*

  action-phase-downloadlink: 
    tc@DownloadLink(call@ActionCall(x_action, args), passign*, elem*) ->
    <for-loop-counter-wrap-code> 
    bstm*|[
        ident = e_ident;
        if(threadLocalPageCached.getParammap().get(ident) != null) // not necessary: && threadLocalPageCached.getParammap().get(ident).equals("1") )
        {
            ~<generate-submit-action-run-invoke(|x_action, e*)>
        }
    ]|
    with  e_ident := <action-id-java-expression(|args, passign*)> tc
        ; <get-anno(?ActionFargAnno(fargcalled))> call
        ; e* := <action-args-to-java-exp-list(|args, fargcalled)>

  //in first traversal, for input data binding, already register which action is executed, in order to know about ignore-validation
  //we need handle-actions traversal as well to get the right arguments for the action, which may depend on input data binding
  databind-phase-downloadlink:
    tc@DownloadLink(ActionCall(x_action, args), passign*, elem*) ->
    <for-loop-counter-wrap-code> 
    bstm*|[
        ident = e_ident;
        if(threadLocalPageCached.getParammap().get(ident) != null)
        {
            ~<generate-submit-action-databind-phase(|x_action)>
        }
    ]|
    with  e_ident := <action-id-java-expression(|args, passign*)> tc

rules //file download

  call-to-java-servlet :
    exp  |[ e.download() ]| -> java |[ ThreadLocalPage.get().setDownload(e2) ]|
    where SimpleSort("File") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e

  call-to-java-servlet :
    exp  |[ e.fileName() ]| -> java |[ e2.getFileName() ]|
    where SimpleSort("File") := <get-type-anno> e
    with  e2 := <expression-to-java-servlet> e
