module org/webdsl/dsl/to-java-servlet/types/string

imports 
  libstrategolib 
  Java-15 
  libjava-front 
  libwebdsl-front

rules
  
  builtin-java-type :
    SimpleSort("String") -> type|[ String ]|

rules

  expression-to-java-servlet :
    String(x) -> expr|[ "~x" ]|
    
rules //native java operations    

 call-to-java-servlet :
   exp |[ e.concat(e1) ]| -> java |[ utils.StringType.concatWithSeparator(e_list,e_sep)  ]|
   where <anno-list-string-type-compatible> e
   with  e_list   := <expression-to-java-servlet> e
       ; e_sep    := <expression-to-java-servlet> e1
       
 call-to-java-servlet :
   exp |[ e.concat() ]| -> java |[ utils.StringType.concat(e_list)  ]|
   where <anno-list-string-type-compatible> e
   with  e_list   := <expression-to-java-servlet> e

 call-to-java-servlet :
   exp  |[ log(e1) ]| ->   java |[ System.out.println(e2) ]|
   where <anno-string-type-compatible> e1
   with  e2 := <expression-to-java-servlet> e1
 call-to-java-servlet :
   exp  |[ log(e1) ]| ->   java |[ System.out.println(""+e2) ]|
   where not(<anno-string-type-compatible> e1)
   with  e2 := <expression-to-java-servlet> e1

 call-to-java-servlet :
   exp  |[ e1.toString() ]| -> java |[ String.valueOf(e2) ]|
   with  e2 := <expression-to-java-servlet> e1
   
