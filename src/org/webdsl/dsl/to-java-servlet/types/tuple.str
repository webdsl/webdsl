module org/webdsl/dsl/to-java-servlet/types/tuple

imports
  libstrategolib
  libjava-front
  libwebdsl-front
  libback-end-transformations
  org/webdsl/dsl/to-java-servlet/-

rules

  builtin-java-type :
    TupleSort(s*) -> type|[ Object[] ]|
    where t* := <map(java-type)> s*

rules

  expression-to-java-servlet :
    TupleCreation(e1*) -> expr|[ new Object[]{ e2* } ]|
    with  e2* := <map(expression-to-java-servlet)> e1*

rules

   expression-to-java-servlet :
     Eq(x, y) -> expr|[ org.webdsl.tools.Utils.equalTuple(e_x,e_y) ]|
     where TupleSort(_) := <get-type-anno> x
     with  e_x := <expression-to-java-servlet> x
         ; e_y := <expression-to-java-servlet> y

   expression-to-java-servlet :
     NotEq(x, y) -> expr|[ !org.webdsl.tools.Utils.equalTuple(e_x,e_y) ]|
     where TupleSort(_) := <get-type-anno> x
     with  e_x := <expression-to-java-servlet> x
         ; e_y := <expression-to-java-servlet> y

rules

  field-access-to-java-servlet :
    FieldAccess(e1, "length") -> expr|[ e2.length ]|
    where TupleSort(_) := <get-type-anno> e1
    with  e2 := <expression-to-java-servlet> e1

  field-access-to-java-servlet :
    FieldAccess(e1, number) -> expr|[ e2[e_index] ]|
    where TupleSort(_) := <get-type-anno> e1
        ; e_index := Lit(Deci(<is-tuple-index-accessor; int-to-string> number))
    with  e2 := <expression-to-java-servlet> e1
