module org/webdsl/dsl/transformation/backend-annotations

imports
  libstrategolib

imports
  libwebdsl-front
  org/webdsl/dsl/transformation/-
  
strategies

  add-backend-annotations =
    topdown(try(type-to-annotation); repeat(add-backend-annotation))
    

rules    // position annotations for assert statements
 
  add-backend-annotation =
      where(keep-position-anno)
    ; where(has-no-anno(?PositionAnno(_)))
    ; add-anno(|PositionAnno(<error-context>))
    
rules
  
  add-global-info :
    a@Application(_,_){anno*} -> a{AllTemplateDefsAnno(renameddef*),anno*}
    with def* := <all-keys-TemplateSignature; mapconcat(bagof-TemplateSignature)> // bagof to also retrieve overrides
       ; emaildef* := <all-keys-EmailDefinition; map(EmailDefinition)>
       ; renameddef* := 
           <map(
             {\ Define(mod*,x,farg*,_,_) -> Define(mod*,x,<map(Arg(add-same-original-name-anno,try(type-to-annotation)))>farg*,[],[]) \} // strip template body
           ; rename-template-def-id // apply rename-identifiers which puts arguments types in template names
           );uniq> //remove duplicates 
             [def*,emaildef*] 
              
   
  add-same-original-name-anno : x{anno*} -> x{OriginalNameAnno(x{}),anno*}         
